/*
 Copyright (c) 2012 Centre National de la Recherche Scientifique (CNRS).

 Licensed under the Apache License, Version 2.0 (the "License");
 you may not use this file except in compliance with the License.
 You may obtain a copy of the License at

 http://www.apache.org/licenses/LICENSE-2.0

 Unless required by applicable law or agreed to in writing, software
 distributed under the License is distributed on an "AS IS" BASIS,
 WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 See the License for the specific language governing permissions and
 limitations under the License.
 */

options {
  LOOKAHEAD = 1;
  FORCE_LA_CHECK = false;
  STATIC = false;
  MULTI = true;
  UNICODE_INPUT = true;
  JDK_VERSION = "1.5";
}

PARSER_BEGIN(CIMIFilterParser)

package eu.stratuslab.cimi;

public class CIMIFilterParser { 

}

PARSER_END(CIMIFilterParser)

SKIP : /* Whitespace. */
{
  < (" " | "\t" | "\f" | "\n" | "\r") >
}

TOKEN : /* Operators and punctuation. */
{
  <LPAREN: "(">
  |
  <RPAREN: ")">
  |
  <OR: "|">
  |
  <AND: "&">
  |
  <GT: ">">
  |
  <LT: "<">
  |
  <EQ: "=">
  |
  <NE: "!=">
  |
  <GE: ">=">
  |
  <LE: "<=">
}

TOKEN : /* Literals. */
{
  < TRUE: "true">
  |
  < FALSE: "false">
  |
  < DECIMAL_LITERAL: (["0"-"9"])+ >
  |
  < SINGLE_QUOTED_STRING: "'" ( "\\'" | "\\\\" | (~["'"]) )* "'" >
  |
  < DOUBLE_QUOTED_STRING: "\"" ( "\\\"" | "\\\\" | (~["\""]) )* "\"" >
}


TOKEN : /* Reserved words and operators. */
{
  <PROPERTY_PREFIX: "property[" >
  | 
  <PROPERTY_SUFFIX: "]" >
}

TOKEN : /* Default identifiers. */
{
  <IDENTIFIER: [ "a"-"z", "A"-"Z", "_" ] ( [ "a"-"z", "A"-"Z", "_", "0"-"9" ] )* >
}

/* If nothing else matches in the lexer, then there is an error.
   Match the character where a standard token cannot be found. */
<*> TOKEN:
{
  <ERROR: ~[] >
}

/*************************************************************************
End-of-file processing.
*************************************************************************/
void endOfFile() #void :
{ }
{
  token=<EOF>
  { if (token.image != null && (! "".equals(token.image))) throw new ParseException(token.image); }
}

/*************************************************************************
Literals.  
*************************************************************************/
void stringValue() #StringValue :
{ Token t; }
{
  ( t=<SINGLE_QUOTED_STRING> | t=<DOUBLE_QUOTED_STRING> )
}

void intValue() #IntValue :
{ Token t; }
{
  ( t=<DECIMAL_LITERAL> )
}

void boolValue() #BoolValue :
{ Token t; }
{
  ( t=<TRUE> | t=<FALSE> )
}

/*************************************************************************
Expressions.
*************************************************************************/

ASTFilter filter() #Filter : 
{ }
{
  logicalOrExpression()
  endOfFile()
  {
    return(jjtThis);
  }
}

void logicalOrExpression() #void : 
{ }
{
  logicalAndExpression() ( <OR> logicalAndExpression() #logicalOrExpression(2) )*
}

void logicalAndExpression() #void : 
{ }
{
  comp() ( <AND> comp() #logicalAndExpression(2) )*
}

void comp() #void : 
{ }
{
  attribute() op() value()
  |
  value() op() attribute()
  |
  <LPAREN> filter() <RPAREN>
}

void attribute() #void :
{ }
{
  attributeName()
  |
  propExpr()
}

void attributeName() #AttributeName :
{ }
{
  <IDENTIFIER>
}

void propExpr() #PropExpr :
{ }
{
  <PROPERTY_PREFIX> stringValue() <PROPERTY_SUFFIX>
}

void op() #void :
{ }
{
  <LT> | <LE> | <EQ> | <GE> | <GT> | <NE>
}

void value() #void :
{ }
{
  intValue()
  |
  stringValue()
  |
  boolValue()
}
